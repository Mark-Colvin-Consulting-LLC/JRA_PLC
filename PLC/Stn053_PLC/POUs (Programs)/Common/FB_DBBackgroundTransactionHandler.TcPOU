<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FB_DBBackgroundTransactionHandler" Id="{cf92dbc3-dc0a-4e9c-b344-355b80fcb9f0}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK FB_DBBackgroundTransactionHandler
VAR_INPUT
	ProjectDBDefs : IProjectDBDefs; //Instance of the custom database FB for this database
END_VAR
VAR_OUTPUT
END_VAR
VAR
	//Property backing variables
	_JRSQLNetId : T_AmsNetID;
	_JRSQLPort : T_AmsPort;
	_DBServer : POINTER TO fb_dbserver;
	_Busy : BOOL;
	_Done : BOOL;
	_Error : BOOL;
	_ErrorId : UINT;	
	_ErrorText : STRING;
	
	//'Framework'
	bFirstScan: BOOL := TRUE;
	tmrState:  TON;

	nState:	UDINT	:= 0;
	nNextState:	UDINT	:= 0;
	bFirstEntry: BOOL;	
	
	//Locals
	Triggered : BOOL := FALSE;
	DataReturn : BOOL := FALSE;
	
	ptrBackgroundItem : POINTER TO DBBackgroundItem;
	
	spName :STRING;
	spData: pvoid;
	spDataSize:UDINT;
	
	spDefinition : DBSPDefinition;
	spCallResult : DBSPCallResult;
	
	TransmitBuffer : POINTER TO BYTE;
	TransmitSize : UINT;
	ReceiveBuffer : POINTER TO BYTE;
	ReceiveSize : UINT;
	
	LastProcessTime : TIME;
		
	fbADSReadWrite: ADSRDWRTEX;

END_VAR
VAR CONSTANT
	cJRSQLTimeout : TIME := T#10S;
	cJRSQLCommandExecute : UDINT := 16#1000;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bFirstScan THEN
	nNextState:=0;
	nState:=9999;
	
	fbADSReadWrite(NETID:=_JRSQLNetId,PORT:=_JRSQLPort,WRTRD:=FALSE,TMOUT:=cJRSQLTimeout);
	bFirstScan := FALSE;
END_IF

//state machine	
IF nState <> nNextState THEN
	bFirstEntry := TRUE;
	nState := nNextState;
ELSE
	bFirstEntry := FALSE;
END_IF

tmrState(IN:=(NOT bFirstEntry), PT:=T#20S);

CASE nState OF
0:
	IF bFirstEntry THEN
		_Error:=FALSE;
		_ErrorId := 16#0;
		_ErrorText := '';
		_Done:=FALSE;
	END_IF

	IF _DBServer^.BackgroundQueue^.Count > 0 THEN
		IF _DBServer^.Connected THEN
			ptrBackgroundItem:= _DBServer^.BackgroundQueue^.Pop();
			nNextState := 40;
		ELSE
			nNextState := 10;
		END_IF
	END_IF
	
10://wait for connection
	IF (_DBServer^.Connected) THEN
		nNextState := 0;
	ELSIF tmrState.ET > cJRSQLTimeout THEN
		_ErrorText:='JRSQL connection timeout';		
		_ErrorId := 16#1;
		_Error:=TRUE;
		nNextState	:= 999;
 	END_IF

//States combined to get rid of scan delays	
40: //Call JRSQL
	IF bFirstEntry THEN
	
		spName := ptrBackgroundItem^.SPName;
		spData := ADR(ptrBackgroundItem^.Data);
		spDataSize := ptrBackgroundItem^.DataSize;
	
		IF(ProjectDBDefs.GetSPDef(spName, ADR(spDefinition))) THEN
			TransmitSize := SIZEOF(DBSPDefinition) + spDefinition.InputParamSize;
			ReceiveSize := SIZEOF(DBSPCallResult) + spDefinition.OutputParamSize;

			IF (spDataSize = spDefinition.InputParamSize+spDefinition.OutputParamSize) THEN
				;
			ELSE
				_ErrorText := CONCAT('Procedure data size mismatch. Expected: ',CONCAT(UINT_TO_STRING(spDefinition.InputParamSize+spDefinition.OutputParamSize),CONCAT(' Got: ',UDINT_TO_STRING(spDataSize))));
				_ErrorId:= 16#3;
				_Error:=TRUE;
				nNextState := 999;
			END_IF
		ELSE 
			_ErrorText:='Stored procedure not found';		
			_ErrorId := 16#2;
			_Error:=TRUE;
			nNextState	:= 999;		
		END_IF	
	
		IF (NOT _Error) THEN
			//Allocate buffers
			TransmitSize := SIZEOF(DBSPDefinition) + spDefinition.InputParamSize;
			TransmitBuffer := __NEW(BYTE, TransmitSize);
			MEMSET(TransmitBuffer,0,TransmitSize);
			ReceiveSize := SIZEOF(DBSPCallResult) + spDefinition.OutputParamSize;
			ReceiveBuffer := __NEW(BYTE, ReceiveSize);
			MEMSET(ReceiveBuffer,0,ReceiveSize);
		
			memcpy(TransmitBuffer,ADR(spDefinition),SIZEOF(DBSPDefinition));
			memcpy((TransmitBuffer + SIZEOF(DBSPDefinition)),spData,spDefinition.InputParamSize);
		END_IF
	END_IF

	IF (NOT _Error) THEN
		
		fbADSReadWrite(WRTRD:=TRUE, IDXGRP:=cJRSQLCommandExecute, WRITELEN:=TransmitSize, SRCADDR:= TransmitBuffer, READLEN:=ReceiveSize, DESTADDR:= ReceiveBuffer);

		IF NOT fbADSReadWrite.Busy THEN
			IF NOT fbADSReadWrite.ERR THEN
				memcpy(ADR(spCallResult),ReceiveBuffer,SIZEOF(DBSPCallResult));
			
				IF spCallResult.Result <> 1 THEN
					//Error returned by JRSQL
					_ErrorText := CONCAT('JRSQL error: ',spCallResult.ResultString);
					_ErrorId:= 16#1000 + spCallResult.Result;
					_Error:=TRUE;
					nNextState := 999;
				ELSE
					//Copy result to caller
					memcpy(spData+spDefinition.InputParamSize,ReceiveBuffer+SIZEOF(DBSPCallResult),spDefinition.OutputParamSize);
					LastProcessTime := spCallResult.ProcessTime;
					_Done:=TRUE;
					nNextState := 999;		
				END_IF
			ELSE
				IF fbADSReadWrite.ERRID = 1861 THEN
					_ErrorText := 'JRSQL response timeout';
					_ErrorId:= 16#8;
					_Error:=TRUE;
					nNextState := 999;	
				ELSE
					_ErrorText := Concat('Error communicating to JRSQL: ', UDINT_TO_STRING(fbADSReadWrite.ERRID));
					_ErrorId:= 16#9;
					_Error:=TRUE;
					nNextState := 999;	
				END_IF
			END_IF
		END_IF
	
		IF (nState <> nNextState) THEN
			fbADSReadWrite(WRTRD:=FALSE);
			__DELETE(TransmitBuffer);
			__DELETE(ReceiveBuffer);
		END_IF
	
	END_IF
	
999:
	IF bFirstEntry THEN
		IF NOT _Error THEN
			_Done:=TRUE;
		END_IF
	END_IF
	
	//free the item from the queue now that we have its data
	__DELETE(ptrBackgroundItem);	
	nNextState	:= 0;	

END_CASE	

_Busy := (nState <> 0);
Triggered:=FALSE;
DataReturn:=FALSE;
]]></ST>
    </Implementation>
    <Property Name="Busy" Id="{191f1f46-55d2-4adb-8792-e68c156c0dfb}">
      <Declaration><![CDATA[PROPERTY Busy : bool]]></Declaration>
      <Get Name="Get" Id="{df7b3220-6c13-47ce-bdf6-7d10c803807c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Done" Id="{efb2e474-2e3b-40d4-a2d5-ab6b3562c210}">
      <Declaration><![CDATA[PROPERTY Done : bool]]></Declaration>
      <Get Name="Get" Id="{7344f72c-5e63-4089-adfa-c5242cc392ec}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Done := _Done;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Error" Id="{6261f579-ac5b-44ca-9bdf-b57ea77659e3}">
      <Declaration><![CDATA[PROPERTY Error : bool]]></Declaration>
      <Get Name="Get" Id="{b5afec79-8e75-4bc4-a2e1-02a416c797f6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorId" Id="{06b7b45f-751a-456d-a951-8ec8903a7e71}">
      <Declaration><![CDATA[PROPERTY ErrorId : UDINT
]]></Declaration>
      <Get Name="Get" Id="{9b8de7f9-44a4-42e0-8299-398a4f0dc9d4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorId := _ErrorId;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorText" Id="{27218892-0c70-4f87-8974-ca3da5d8132c}">
      <Declaration><![CDATA[PROPERTY ErrorText : STRING]]></Declaration>
      <Get Name="Get" Id="{8dabe950-fabc-470a-b1d8-2792b43761f7}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorText := _ErrorText;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_init" Id="{7aee4319-4ae4-43ba-9e5c-e8d4c3e8dd5c}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	
	pDBServer : POINTER TO FB_DBServer;
	JRSQLNetId : T_AmsNetID := ''; //AMS address of JRSQL
	JRSQLPort : T_AmsPort := 2020; //AMS port of JRSQL
	ProjectDBDefs : IProjectDBDefs; //Project database config
	
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_DBServer := pDBServer;
_JRSQLNetId := JRSQLNetId;
_JRSQLPort := JRSQLPort;
THIS^.ProjectDBDefs := ProjectDBDefs;

IF bInitRetains THEN
	bFirstScan := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_DBBackgroundTransactionHandler">
      <LineId Id="3" Count="146" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.Busy.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.Done.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.Error.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.ErrorId.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.ErrorText.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_DBBackgroundTransactionHandler.FB_init">
      <LineId Id="3" Count="6" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>