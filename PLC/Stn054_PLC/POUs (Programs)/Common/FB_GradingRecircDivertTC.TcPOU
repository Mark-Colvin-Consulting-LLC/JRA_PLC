<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FB_GradingRecircDivertTC" Id="{109b6e4d-0325-4dd8-8db5-f4730612d880}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_GradingRecircDivertTC
VAR_INPUT
	pCmp: POINTER TO CmpConvDivert;
	DivertOnlyExceptions: BOOL;
	InBypassThisStn: BOOL;
END_VAR
VAR
	MainRunning: BOOL;
	SpurRunning: BOOL;
	onsPuckReady: R_TRIG;
	ResolvedDBStatusRouteCleanGreen: cResolvedDBStatusRoute;
	ResolvedDBStatusRouteGradingGreen: cResolvedDBStatusRoute;
	ResolvedDBStatusRouteTestRed: cResolvedDBStatusRoute;
	DBRouteToSpur_Empty: BOOL;
	DBRouteToSpur_Full: BOOL;
	RouteToSpur: BOOL;
	ReleaseToNotBlockMain: TON;
	onsDivertActedOnRelease: R_TRIG;
	LastEmptyWasSentToMain: BOOL;
	Debug_DivertAllToMain: BOOL;
	Debug_DivertAllToSpur: BOOL;
	Debug_DivertEmpties: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Traffic control for specific dirvert - GradingRecirc
// 
// Currently each divert has independent/unique function block as it is expected that they will
//   eventually need to be tweeked for specific application

// Sum of main/spur running conditions... don't release if not running
MainRunning :=
	pCmp^.MainHeartbeatOK AND
	pCmp^.MainConvRunning;

SpurRunning :=
	pCmp^.SpurHeartbeatOK AND
	pCmp^.SpurConvRunning;
	
// On falling edge of request note if status of puck indicates that it could go to spur
//   and set bit based on which way it was actually told to release.
// Note: this logic must be above the decision logic so it reflects the result from last scan
onsDivertActedOnRelease(CLK:=NOT pCmp^.PuckReady);
IF onsDivertActedOnRelease.Q AND RouteToSpur THEN
	LastEmptyWasSentToMain := pCmp^.PuckReleaseMain;	
END_IF;		

// On rising edege of puck ready interperate the puck/device data to see if it should be diverted to spur
onsPuckReady(CLK:=pCmp^.PuckReady);
IF onsPuckReady.Q THEN 	
	// Call function to turn DB puck/device status/route into single readable resolution
	ResolvedDBStatusRouteCleanGreen :=
		FN_ResolveDBStatusRoutePuck(
			Puck := pCmp^.Puck,
			RouteToWork := cDBDetailRoute.CleaningGreen);
			
		ResolvedDBStatusRouteGradingGreen :=
		FN_ResolveDBStatusRoutePuck(
			Puck := pCmp^.Puck,
			RouteToWork := cDBDetailRoute.GradingGreen);
	
		ResolvedDBStatusRouteTestRed :=
		FN_ResolveDBStatusRoutePuck(
			Puck := pCmp^.Puck,
			RouteToWork := cDBDetailRoute.TestGreen);
		
	// Use DB route and other possible line status to determine if puck should go to spur
	DBRouteToSpur_Empty := 
		ResolvedDBStatusRouteCleanGreen = cResolvedDBStatusRoute.Empty_NoRoute OR
		ResolvedDBStatusRouteCleanGreen = cResolvedDBStatusRoute.Empty_RoutedThisStation OR
		ResolvedDBStatusRouteCleanGreen = cResolvedDBStatusRoute.Empty_RoutedOtherStation;
	
	DBRouteToSpur_Full := 
		ResolvedDBStatusRouteCleanGreen = cResolvedDBStatusRoute.Normal_RoutedThisStation OR
		ResolvedDBStatusRouteGradingGreen = cResolvedDBStatusRoute.Normal_RoutedThisStation OR
		ResolvedDBStatusRouteTestRed = cResolvedDBStatusRoute.Normal_RoutedThisStation;
END_IF

//  Note that only "full" puck receving as determined above are headed to exception which only occurs on pilot)
//    Also only for pilot allow the station to request blcoking empties/only divet fulls if idle so exception have easy room to come off)
RouteToSpur := 
	DBRouteToSpur_Full OR
	(DBRouteToSpur_Empty AND
	Debug_DivertEmpties);

// For CRTC1 we must keep pucks circulating at full speed since there are so many downstream stations that a simple
//   buffer full type check will not work.  If a full set of status and data is avaiable in the future then something
//   may be able to make a complex choice... but not here, not now.
// For pilot, since it is linear and less complex so some slight delay for now...	
ReleaseToNotBlockMain(IN := (NOT SpurRunning OR pCmp^.SpurBufferFull),
	PT := T#2S);
	
// Release main if DB route is not to spur, or even if it is to spur when this station is bypassed,
//   or release to not block main line traffic, or if CRTC1 to release eveey other spur heading puck downstream
//   for partner station to have work.
pCmp^.PuckReleaseMain := 
	pCmp^.PuckReady AND
	MainRunning AND
	((NOT RouteToSpur OR
	(RouteToSpur AND 
	(InBypassThisStn OR
	ReleaseToNotBlockMain.Q))) OR
	Debug_DivertAllToMain) AND
	NOT Debug_DivertAllToSpur;

// Release to spur if that is route and it is available.
//    and not trying to keep downstream station fed.
pCmp^.PuckReleaseSpur :=
	pCmp^.PuckReady AND
	MainRunning AND
	SpurRunning AND
	((RouteToSpur AND
	NOT InBypassThisStn) OR
	Debug_DivertAllToSpur) AND
	NOT Debug_DivertAllToMain;
	]]></ST>
    </Implementation>
    <LineIds Name="FB_GradingRecircDivertTC">
      <LineId Id="3" Count="21" />
      <LineId Id="52" Count="7" />
      <LineId Id="183" Count="0" />
      <LineId Id="185" Count="2" />
      <LineId Id="184" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="190" Count="2" />
      <LineId Id="189" Count="0" />
      <LineId Id="60" Count="7" />
      <LineId Id="71" Count="0" />
      <LineId Id="194" Count="1" />
      <LineId Id="72" Count="5" />
      <LineId Id="79" Count="1" />
      <LineId Id="82" Count="0" />
      <LineId Id="104" Count="4" />
      <LineId Id="259" Count="0" />
      <LineId Id="113" Count="0" />
      <LineId Id="121" Count="9" />
      <LineId Id="132" Count="10" />
      <LineId Id="144" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>